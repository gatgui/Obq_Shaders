# SPDL Obq_Bend4Stereo - Created by Marc-Antoine Desjardins - ObliqueFX - marcantoinedesjardins@gmail.com : Licensed under the MIT license: http://www.opensource.org/licenses/mit-license.php
SPDL
Version = "2.0.0.0";
Reference = "{27F1927B-7363-4D31-B7D2-197B9A792349}";
PropertySet "Obq_Bend4Stereo"
{
	Parameter "out" output
	{
		GUID = "{14B9E211-8A87-4CCC-BD34-7ECBCCB7AD74}";
		Type = color;
		flags = 1;
	}
	Parameter "shaderIn" input
	{
		GUID = "{FABF6D1F-75CB-4F0E-918A-E7F9AE50678E}";
		Type = color;
		Value = 1.0 1.0 1.0 1.0;
		Texturable = on;
	}
	Parameter "bendMode" input
	{
		GUID = "{AAC54765-C318-4FC9-9312-601D3577CB97}";
		Type = string;
		flags = 1;
		Value = "Don't bend";
		Texturable = Off;
		Animatable = On;
	}
	Parameter "leftCamera" input
	{
		GUID = "{49B6ED2E-2191-4556-832A-E3C3BD165D63}";
		Type = string;
		flags = 1;
		Value = "StereoCamera_Left";
		Texturable = Off;
		Animatable = On;
	}
	Parameter "centerCamera" input
	{
		GUID = "{0A2AA60B-CF18-4173-BCAD-E0D2A498F223}";
		Type = string;
		flags = 1;
		Value = "StereoCamera";
		Texturable = Off;
		Animatable = On;
	}
	Parameter "rightCamera" input
	{
		GUID = "{87EBC3F0-C9D5-48D9-8ACC-C0DAE930BF0F}";
		Type = string;
		flags = 1;
		Value = "StereoCamera_Right";
		Texturable = Off;
		Animatable = On;
	}
	Parameter "cameraStatus" input
	{
		GUID = "{84177A8B-8354-4821-A11E-6B003961E98E}";
		Type = string;
		flags = 1;
		Value = "No Camera Associated";
		Animatable = Off;
	}
}

phenomenon "Obq_Bend4Stereo_declare"
{
	Name = "Obq_Bend4Stereo";
	Version = 1;
	Use = texture;
}

Plugin = Shader
{
	FileName = "Obq_Bend4Stereo";
}

Defaults
{
	shaderIn
	{
		Name = "Shader Port";
		Commands = "{F5C75F11-2F05-11d3-AA95-00AA0068D2C0}";
		UIType = "rgba";
	}
	bendMode
	{
		Name = "Bend Mode";
		Commands = "{F5C75F11-2F05-11d3-AA95-00AA0068D2C0}";
		UIType = "Combo";
		Items
		{
			"Don't bend" = "Don't bend",
			"Bend from left" = "Bend from left",
			"Bend from right" = "Bend from right",
			"Bend from center" = "Bend from center"
		}
	}
	leftCamera
	{
		Name = "Left";
		Commands = "{F5C75F11-2F05-11d3-AA95-00AA0068D2C0}";
	}
	centerCamera
	{
		Name = "Center";
		Commands = "{F5C75F11-2F05-11d3-AA95-00AA0068D2C0}";
	}
	rightCamera
	{
		Name = "Right";
		Commands = "{F5C75F11-2F05-11d3-AA95-00AA0068D2C0}";
	}
}

Layout "Default"
{
	Tab "Main"
	{
		Group "Bend Mode"
		{
			bendMode;
		}
		Group "Cameras"
		{
			Button pickCameras,aligncenter = "Pick Cameras",180;
			centerCamera;
			Row
			{
				leftCamera;
				rightCamera;
			}
			cameraStatus;
		}
	}
}

Layout "RenderTree"
{
		shaderIn;
}

Language = "VBScript";
Logic
{	
	Sub checkCameras()
		Set oLCamera = Dictionary.GetObject(PPG.leftCamera.Value & "",false)
		Set oCCamera = Dictionary.GetObject(PPG.centerCamera.Value & "",false)
		Set oRCamera = Dictionary.GetObject(PPG.rightCamera.Value & "",false)
		If TypeName( oLCamera ) = "Nothing" AND TypeName( oCCamera ) = "Nothing" AND TypeName( oRCamera ) = "Nothing" Then
			PPG.cameraStatus.Value = "!! ALL cameras are NOT valid !!"
		ElseIf TypeName( oLCamera ) = "Nothing" AND TypeName( oCCamera ) = "Nothing" Then
			PPG.cameraStatus.Value = "!! Only Right camera is valid !!"
		ElseIf TypeName( oRCamera ) = "Nothing" AND TypeName( oCCamera ) = "Nothing" Then
			PPG.cameraStatus.Value = "!! Only Left camera is valid !!"
		ElseIf TypeName( oRCamera ) = "Nothing" AND TypeName( oLCamera ) = "Nothing" Then
			PPG.cameraStatus.Value = "!! Only Center camera is valid !!"
		ElseIf TypeName( oRCamera ) = "Nothing" then
			PPG.cameraStatus.Value = "!! Right camera is NOT valid !!"
		ElseIf TypeName( oLCamera ) = "Nothing" then
			PPG.cameraStatus.Value = "!! Left camera is NOT valid !!"
		ElseIf TypeName( oCCamera ) = "Nothing" then
			PPG.cameraStatus.Value = "!! Center camera is NOT valid !!"
		Else
			PPG.cameraStatus.Value = "All cameras are valid."
		End If
	End Sub

	Sub leftCamera_OnChanged()
		checkCameras()
	End Sub
	
	Sub centerCamera_OnChanged()
		checkCameras()
	End Sub

	Sub rightCamera_OnChanged()
		checkCameras()
	End Sub

	Sub pickCameras_OnClicked()	
		dim leftC, rightC, centerC, button
		DeselectAll
		PickObject "Select Center Camera", "Select Center Camera", centerC, button
		if button <> 0 then
			PickObject "Select Left Camera", "Select Left Camera", leftC, button
			if button <> 0 then
				PickObject "Select Right Camera", "Select Right Camera", rightC, button
				if button <> 0 then
					PPG.leftCamera.Value = leftC
					PPG.centerCamera.Value = centerC
					PPG.rightCamera.Value = rightC
					checkCameras()
				end if
			end if
		end if
	End Sub
	
	Sub OnInit
		PPG.PPGLayout.SetAttribute "HelpFile","http://s3aws.obliquefx.com/public/shaders/help_files/Obq_Bend4Stereo.html"
		'Setting Expressions
		checkCameras()
		PPG.cameraStatus.ReadOnly = True
		PPG.Refresh
	End Sub
}
